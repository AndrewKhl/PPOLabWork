apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    def getDescribeTags = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }

    def getVersionCode = { ->
        try {
            def stdout = getDescribeTags()
            def describeTags = stdout.toString().trim().split("-")
            def describeVersion = describeTags[0].toString().trim().split(".")
            def versionMajor = describeVersion[0] as int
            def versionMinor = describeVersion[1] as int
            def patch = describeTags[1] as int
            return versionMajor * 1000 + versionMinor * 100 + patch
        }
        catch (ignored) {
            return 0
        }
    }

    def getVersionName = { ->
        try {
            def stdout = getDescribeTags()
            def describeTags = stdout.toString().trim().split("-")
            return describeTags[0].toString() + "." + describeTags[1].toString()
        }
        catch (ignored) {
            return null
        }
    }

    compileSdkVersion 28
    defaultConfig {
        setProperty("archivesBaseName", "$applicationId-$versionName")
        applicationId "com.example.projectonppo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "version"
    productFlavors {
        ready {
            dimension "version"
            signingConfig signingConfigs.config
            manifestPlaceholders.screenOrientation = "portrait"
        }
        developer {
            dimension "version"
            versionNameSuffix "-dev"
            manifestPlaceholders.screenOrientation = "unspecified"
            signingConfig signingConfigs.config
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'

    def nav_version = "1.0.0-alpha07"
    implementation "android.arch.navigation:navigation-fragment:$nav_version" // use -ktx for Kotlin
    implementation "android.arch.navigation:navigation-ui:$nav_version" // use -ktx for Kotlin

    implementation "com.google.firebase:firebase-core:16.0.5"
    implementation 'com.google.firebase:firebase-firestore:17.1.3'
    implementation 'com.google.firebase:firebase-database:16.0.5'
    implementation 'com.google.firebase:firebase-auth:16.0.5'
}
